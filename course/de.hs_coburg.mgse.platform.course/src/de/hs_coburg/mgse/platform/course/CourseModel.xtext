grammar de.hs_coburg.mgse.platform.course.CourseModel with org.eclipse.xtext.common.Terminals

generate courseModel 'http://www.hs_coburg.de/mgse/platform/course/CourseModel'

import 'http://www.hs_coburg.de/mgse/platform/glossary/GlossaryModel' as glossary


Model:
	Faculty | DegreeList
;

DegreeList:
	 {DegreeList}
	 'Abschlussarten' '['
	 	(degreeClasses+=DegreeClass)*
	 ']'
	 'Abschlussunterarten' '['
	 	(degrees+=Degree)*
	 ']'
;

DegreeClass:
	'-' '[' name=ID ']' ('(' completeName=STRING ')')?
;
Degree:
	'-' '[' name=ID ']:'reference=[glossary::GlossaryEntry|FQN] 
;

Professor:
	'Professor' '[' name=ID ']'
		'-' 'Vorname' firstName=STRING
		('-' 'Zweitname' middleName=STRING)?
		'-' 'Nachname' lastName=STRING
		'-' 'Email' email=STRING
		'-' 'Raum' room=STRING
;

Faculty:
	'Fakultaet' completeName=STRING '[' name=ID ']'
	'Studiengaenge' '['
		(courses+=CourseOfStudies)*
	']'
	'Professoren' '['
		(professors+=Professor)+
	']'
;

CourseOfStudies:
	'Studiengang' completeName=STRING '[' name=ID ']'
	('Zulassungsvoraussetzung' requirements+=AdmissionRequirement (',' vals+=AdmissionRequirement)*)?
	'ECTS' ects=INT
	'Semester' semester=INT
	'Abschluss' degreeClass=[DegreeClass|FQN] 'of' degree=[Degree]
;

AdmissionRequirement:
	value+=STRING
;

FQN: ID (('.') ID)*;
