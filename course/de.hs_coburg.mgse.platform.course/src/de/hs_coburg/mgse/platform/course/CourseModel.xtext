grammar de.hs_coburg.mgse.platform.course.CourseModel with org.eclipse.xtext.common.Terminals

generate courseModel 'http://www.hs_coburg.de/mgse/platform/course/CourseModel'

import 'http://www.hs_coburg.de/mgse/platform/glossary/GlossaryModel' as glossary


Model:
	Faculty | DegreeList | AdmissionRequirementList
;

AdmissionRequirementList:
	'Zulassungsvoraussetzungen' '['
		(requirements+=AdmissionRequirement)+
	']'
;

DegreeList:
	 {DegreeList}
	 'Abschlussarten' '['
	 	(degreeClasses+=DegreeClass)*
	 ']'
	 'Abschlussunterarten' '['
	 	(subDegrees+=SubDegree)*
	 ']'
	 'Abschluesse' '['
	 	(degrees+=Degree)*
	 ']'
;

DegreeClass:
	'-' '[' name=ID ']:' completeName=STRING
;
SubDegree:
	'-' '[' name=ID ']:' completeName=STRING
;

Degree:
	'-' '[' name=ID ']:' degreeClass=[DegreeClass|FQN] 'of' degree=[SubDegree|FQN] ('[Details:' glossaryEntry=[glossary::GlossaryEntry|FQN] ']')?
;

Professor:
	'Professor' '[' name=ID ']'
		'-' 'Vorname' firstName=STRING
		('-' 'Zweitname' middleName=STRING)?
		'-' 'Nachname' lastName=STRING
		'-' 'Email' email=STRING
		'-' 'Raum' room=STRING
		'-' 'Kuerzel' abbreviation=[glossary::GlossaryEntry|FQN]
;

Faculty:
	'Fakultaet' completeName=STRING '[' name=ID ']'
	'Studiengaenge' '['
		(courses+=CourseOfStudies)*
	']'
	'Professoren' '['
		(professors+=Professor)+
	']'
;

CourseOfStudies:
	'Studiengang' completeName=STRING '[' name=ID ']'
	('Zulassungsvoraussetzung' (requirements+=[AdmissionRequirement|FQN])+ (',' requirements+=[AdmissionRequirement|FQN])*)?
	'ECTS' ects=INT
	'Semester' semester=INT
	'Abschluss' degree=[Degree|FQN]
;



AdmissionRequirement:
	'-' '[' name=ID ']:' value+=STRING
;

FQN: ID (('.') ID)*;
