grammar de.hs_coburg.mgse.platform.ser.SERModel with org.eclipse.xtext.common.Terminals

generate sERModel "http://www.hs_coburg.de/mgse/platform/ser/SERModel"
import 'http://www.hs_coburg.de/mgse/platform/glossary/GlossaryModel' as glossary
import 'http://www.hs_coburg.de/mgse/platform/course/CourseModel' as course

Model:
	StudyExaminationRegulations | Module | ExamType | ExamTypeDeclaration
;

StudyExaminationRegulations:
	'ID' name=ID
	'Studiengang' course=[course::CourseOfStudies|FQN]
	'Version' version=INT // Should be used as Primary Key
	'Gueltigkeitsdatum' date=DATE // Should be used as Primary Key
	'Titel' title=STRING
	'Vorwort' foreword=STRING
	'Paragraphen' '['
		(paragraphs+=Paragraph)+
	']'
	('Fussnoten' '['
		(footnotes+=Footnote)*
	']')?
	'Studienabschnitte' '['
		(studySections+=StudySection)+
	']'
;

StudySection:
	'Startsemester' firstSemester=INT
	'Endsemester' lastSemester=INT
	'Bezeichnung' completeName=STRING
	'Modules' '['
		(modules+=[Module|FQN])+
	']' 
;

Module:
	'Bezeichnung' completeName=STRING
	'ECTS' ects=INT
	'Gewichtung' quantifier=DOUBLE
	'SWS' semesterHours=INT
	'Modulart' moduleType=STRING
	(('Details' details=[glossary::GlossaryEntry|FQN]) | ('Anzahl' count=INT))
	'Pruefungsarten' '['
		(examTypes+=[ExamType|FQN])*
	']'
;

ExamType: 
	'Pruefungsart' examTypeDeclaration=[ExamTypeDeclaration|FQN]
	'Von' lowerBound=INT
	'Bis' upperBound=INT
;

ExamTypeDeclaration:
	'Einheit' unit=STRING
	'Details' details=[glossary::GlossaryEntry|FQN]
;

Footnote:
	'[' name=ID ']:' text=STRING
;

Paragraph:
	'Start'
		'§' number=INT
		'Titel' title=STRING
		((sentences+=Sentence)+ | (subParagraphs+=SubParagraph)+)
	'Ende'
	
;

SubParagraph:
	'§§' number=INT
	(sentences+=Sentence)+
;

Sentence:
	'-' text=RichText
	('[' 'Fussnoten:' footnotes+=[Footnote|FQN] (',' footnotes+=[Footnote|FQN])* ']')?
;

RichText:
	(literals+=RichTextLiteral)+
;

RichTextLiteral:
	text=STRING | RichTextReference
;

RichTextReference:
	reference=[glossary::GlossaryEntry|FQN]
;

DATE:
	day=INT '.' month=Month year=INT
;

enum Month:
	JANUARY='Januar' 	| FEBRUARY='Februar'	| MARCH='Maerz' |
	APRIL='April' 		| MAY='Mai' 			| JUNE='Juni' |
	JULY='Juli'			| AUGUST='August'		| SEPTEMBER='September' |
	OCTOBER='Oktober'	| NOVEMBER='November'	| DECEMBER='Dezember'
;

FQN: ID (('.') ID)*;

DOUBLE: INT (('.') INT)?;