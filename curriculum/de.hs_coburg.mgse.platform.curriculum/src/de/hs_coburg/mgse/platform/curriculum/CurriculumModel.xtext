grammar de.hs_coburg.mgse.platform.curriculum.CurriculumModel with org.eclipse.xtext.common.Terminals

generate curriculumModel "http://www.hs_coburg.de/mgse/platform/curriculum/CurriculumModel"
import 'http://www.hs_coburg.de/mgse/platform/glossary/GlossaryModel' as glossary
import 'http://www.hs_coburg.de/mgse/platform/course/CourseModel' as course
import 'http://www.hs_coburg.de/mgse/platform/ser/SERModel' as ser

Model:
	Curriculum | AidList;

Curriculum:
	'Studienplan' '[' name=ID ']:' completeName=STRING
	'SPO' ser=[ser::StudyExaminationRegulations|FQN] 
	'Version' version=INT
	'Semester' semester=Semester year=INT
	'Eintraege' '['
		(curriculumEntries+=CurriculumEntry)+
	']'
;

CurriculumEntry:
	'Eintrag' '[' name=ID ']'
	'-' 'Semester' semester=INT
	'-' 'Turnus' rota=Rota
	moduleSpecification=ModuleSpecification
	('-' 'Hilfsmittel' ((customAids+=STRING)|(aids+=[Aid|FQN])) (',' ((customAids+=STRING)|(aids+=[Aid|FQN])))*)?
	'-' 'Pruefer' (testers+=[course::Professor|FQN]) (',' testers+=[course::Professor|FQN])*
;

ModuleSpecification:
	'-' 'Modul' module=[ser::Module|FQN]
	('-' 'Konkreter_Name' completeName=STRING
	'-' 'Details' details=[glossary::GlossaryEntry|FQN])?
	'-' 'Leistungsnachweis' (concreteExamTypes+=ConcreteExamType) (',' concreteExamTypes+=ConcreteExamType)*
	'-' 'Lehrveranstaltungsart' (courseTypes+=[ser::CourseTypeDeclaration|FQN]) (',' courseTypes+=[ser::CourseTypeDeclaration|FQN])*
;

Aid:
	'-' '[' name=ID ']:' completeName=STRING
;

AidList:
	'Hilfsmittel' '['
		(aidDefinitions+=Aid)+
	']'
;

ConcreteExamType:
	examType=[ser::ExamType|FQN] '(' value=INT ')'
;

enum Semester:
	SUMMER_SEMESTER='Sommersemester' 	| WINTER_SEMESTER='Wintersemester'
;

enum Rota:
	YEARLY='jaehrlich' | HALF_YEARLY='halbjaehrlich'
;

FQN: ID (('.') ID)*;